dnl ./configure.in
dnl AutoConf configuration script for the DumbOP package
dnl Author: Simon Butcher
dnl Copyright (c) 2001 Alien Internet Services


dnl -------
dnl Startup
dnl -------
AC_PREREQ(2.13)
AC_INIT(src/Makefile.in)


dnl -------------------------------
dnl Our header of defines to create
dnl -------------------------------
AC_CONFIG_HEADER(src/include/config.h)


dnl -------------------
dnl Checks for programs
dnl -------------------
AC_PROG_CC
AC_PROG_CXX
AC_PROG_MAKE_SET
AC_CHECK_PROG(STRIP, strip, strip, :)
AC_CHECK_PROG(RM, rm, rm -vf, del)
AC_CHECK_PROG(LD, ld, ld)


dnl --------------------
dnl Checks for libraries
dnl --------------------
dnl (none)


dnl -----------------------
dnl Checks for header files
dnl -----------------------
AC_HEADER_STDC
AC_TIME_WITH_SYS_TIME
AC_HEADER_SYS_WAIT
AC_HEADER_DIRENT
AC_CHECK_HEADERS(fcntl.h limits.h sys/time.h unistd.h)


dnl -------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics
dnl -------------------------------------------------------------
AC_C_BIGENDIAN
AC_C_CONST
AC_C_INLINE
AC_C_CHAR_UNSIGNED
AC_C_LONG_DOUBLE
AC_HEADER_STAT
AC_HEADER_TIME
AC_STRUCT_TM
AC_STRUCT_TIMEZONE
AC_DECL_SYS_SIGLIST
AC_TYPE_SIZE_T
AC_TYPE_PID_T
AC_TYPE_MODE_T


dnl ----------------------------
dnl Checks for library functions
dnl ----------------------------
AC_TYPE_SIGNAL
AC_CHECK_FUNCS(ftime select socket)


dnl ---------------------
dnl Check system services
dnl ---------------------
AC_EXEEXT
AC_OBJEXT


dnl ---------------------------
dnl Run local test programs (C)
dnl ---------------------------
AC_MSG_CHECKING(for struct sigcontext in signal handlers)
AC_TRY_COMPILE([#include <sys/signal.h>],[struct sigcontext c;], [AC_DEFINE(HAVE_STRUCT_SIGCONTEXT) AC_MSG_RESULT(yes)], [AC_MSG_RESULT(no)])
AC_MSG_CHECKING(whether signal SIGCHLD is brain-damaged)
AC_TRY_RUN([
	#include <signal.h>

	#ifndef SIGCHLD
	# define SIGCHLD SIGCLD
	#endif
	
	int level = 0;
	
	/* Note - void input because it varies and we don't need it anyway */
	void *handler(void)
	{
	   int status;
	   
	   if (level++ > 2)
	     exit(1);
	   
	   signal(SIGCHLD, handler);
	   wait(&status);
	}
	
	int main(void)
	{
	   signal(SIGCHLD, handler);
	   if (fork())
	     sleep(10); 
	   else {
	      sleep(2);
	      exit(1); 
	   }
	   exit(0);
	}
],[AC_MSG_RESULT([no (good)])],[AC_DEFINE(SIGCHLD_BRAIN_DAMAGE) AC_MSG_RESULT(yes)])


dnl -----------------------------
dnl Run local test programs (C++)
dnl -----------------------------
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(for a compliant STL C++ class framework)
AC_TRY_RUN([
	#include <stl.h>
	
	int main(void)
	{
	   list< char > l;
	   return 0;
	}
],[AC_DEFINE(HAVE_CXX_STL) AC_MSG_RESULT(yes)], [AC_MSG_ERROR([Sorry, you need a compliant Standard Template Library for C++])])


dnl --------------------------------
dnl Make the files we needed to edit
dnl --------------------------------
AC_OUTPUT(Makefile \
src/Makefile \
src/bot/Makefile \
src/common/Makefile \
src/common/strings/Makefile
src/dcc/Makefile \
src/games/Makefile \
src/games/quiz/Makefile \
src/garbage/Makefile \
src/irc/Makefile \
src/irc/channels/Makefile \
src/irc/commands/Makefile \
src/irc/commands/channel/Makefile \
src/irc/commands/connection/Makefile \
src/irc/commands/dcc/Makefile \
src/irc/commands/game/Makefile \
src/irc/commands/general/Makefile \
src/irc/commands/manager/Makefile \
src/irc/commands/user/Makefile \
src/irc/parser/Makefile \
src/irc/parser/user/Makefile \
src/irc/queue/Makefile \
src/irc/queue/items/Makefile \
src/irc/server/Makefile \
src/notes/Makefile \
src/smtp/Makefile \
src/snmp/Makefile \
src/system/Makefile \
src/telnet/Makefile \
src/telnet/ansi/Makefile \
src/telnet/descriptor/Makefile \
src/telnet/dialogue/Makefile \
src/telnet/dialogue/login/Makefile \
src/telnet/dialogue/monitor/Makefile \
src/users/Makefile)


dnl ------------------------------------------
dnl Print a pretty message for the nice people
dnl ------------------------------------------
echo "Phew! Have a nice day! :)"
